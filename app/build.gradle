plugins {
	id 'com.android.application'
	////////// 1. simple jacoco plugin //////////
	id 'jacoco'

	////////// 2. move sonarqube plugin into app/build.gradle //////////
	id 'org.sonarqube' version '3.3'
	id 'com.diffplug.spotless' version '6.17.0'
}

android {
	namespace 'at.vunfer.openrealms'
	compileSdk 33

	defaultConfig {
		applicationId "at.vunfer.openrealms"
		minSdk 29
		targetSdk 33
		versionCode 1
		versionName "1.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
		debug {
			testCoverageEnabled = true
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_9
		targetCompatibility JavaVersion.VERSION_1_9
	}

	////////// 4. add testOptions //////////
	testOptions {
		unitTests.all {
			useJUnitPlatform()
			finalizedBy jacocoTestReport
		}
	}
}
def instrumentationFileName;
////////// 3. add jacocoTestReport task like so //////////
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest','createDebugCoverageReport']) {

	reports {
		xml.enabled true

		//////////  5. xml.destination must be same as sonaqube property  //////////
		xml.destination file("${project.projectDir}/build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml")
	}

	def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
	def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug", excludes: fileFilter)
	def mainSrc = "${project.projectDir}/src/main/java"

	sourceDirectories.from = files([mainSrc])
	classDirectories.from = files([debugTree])
	executionData.from = files("${buildDir}/jacoco/testDebugUnitTest.exec")
	def files = fileTree("${buildDir}/outputs/code-coverage/connected/flavors/DEV/").filter { it.isFile() }.files.name
	instrumentationFileName = "${buildDir}/outputs/code-coverage/connected/flavors/DEV/" + files[0];
}

////////// 2. move also sonarqube properties //////////
sonarqube {
	properties {
		property "sonar.projectKey", "Ne0Strix_open-realms"
		property "sonar.organization", "ne0strix"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.coveragePlugin", "jacoco"

		////////// 5. property same as xml.destination of jacocoTestReport //////////
		property "sonar.coverage.jacoco.xmlReportPaths", "${project.projectDir}/build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
		property "sonar.jacoco.itReportPath", instrumentationFileName
	}
}

dependencies {

	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.8.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
	testImplementation 'org.testng:testng:6.9.6'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore', '**.gradle'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		target 'src/*/java/**/*.java'
		removeUnusedImports()
		cleanthat()
		googleJavaFormat('1.16.0').aosp().reflowLongStrings().groupArtifact('com.google.googlejavaformat:google-java-format')
		formatAnnotations()
		licenseHeader '/* Licensed under GNU GPL v3.0 (C) $YEAR */'
	}
}
